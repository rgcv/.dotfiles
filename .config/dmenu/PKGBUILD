# Maintainer: Rui Ventura <rventura.pt@outlook.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>

pkgname=dmenu
pkgver=4.9
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')

_patches=(
  lineheight-$pkgver.patch::https://tools.suckless.org/dmenu/patches/line-height/dmenu-lineheight-$pkgver.diff
  fuzzymatch-$pkgver.patch::https://tools.suckless.org/dmenu/patches/fuzzymatch/dmenu-fuzzymatch-$pkgver.diff
)

source=(
  https://dl.suckless.org/tools/$pkgname-$pkgver.tar.gz
  ${_patches[@]}
  config.h
)
sha512sums=('c2779209fe012de8ca1cdd72923da6d594f4a8368c85c3c0e0afd4ae489a95fe0e6f05a947d115b6b389aa7170ab14c2c645a2031353b0a08f38327ab461fe65'
            'b2fa2b33b9a2391285d0c3e5df22a9d68f4193bebb4720ce71678e8a87f43ae6c0a6b3f3fd3f63aa89be95880c04c8a83e43240b6ae4f90487bdb5a9325d084c'
            '6ce4ce0ddb9c1058f253ce46cdae8949c468d97ce7d787831d8a4f4ebd4db672761e2552703f58d36d01b2933dea624a30627629027b9a5960b404542a40a732'
            '47a660ae6b77cc304fb482bef5bd9315df8663174759728a41238e5ccadd87a9e52b74384bb22fec45bdc834da02693ccdb6361a2370364694d151681504bf2f')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  {
    echo "CPPFLAGS+=$CPPFLAGS"
    echo "CFLAGS+=$CFLAGS"
    echo "LDFLAGS+=$LDFLAGS"
  } >> config.mk
  for p in "${_patches[@]}"; do
    patch --input="$srcdir/${p%::*}"
  done
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    install
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
