# Maintainer: Rui Ventura <rventura.pt@outlook.com>
pkgname=st
pkgver=0.8.1
pkgrel=1
pkgdesc="A simple virtual terminal emulator for X."
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses')
url="https://st.suckless.org"

_patches=(
    alpha-$pkgver.patch::https://st.suckless.org/patches/alpha/$pkgname-alpha-$pkgver.diff
    anysize-$pkgver.patch::https://st.suckless.org/patches/anysize/$pkgname-anysize-$pkgver.diff
    clipboard-$pkgver.patch::https://st.suckless.org/patches/clipboard/$pkgname-clipboard-$pkgver.diff
    scrollback-$pkgver.patch::https://st.suckless.org/patches/scrollback/$pkgname-scrollback-$pkgver.diff
)

source=(
  https://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
  "${_patches[@]}"
  config.h
)
sha256sums=('c4fb0fe2b8d2d3bd5e72763e80a8ae05b7d44dbac8f8e3bb18ef0161c7266926'
            '7bf61cb8a505891574f3ad0a5420334d3e965b9f7d0067df3819eeef72ce1358'
            '8118dbc50d2fe07ae10958c65366476d5992684a87a431f7ee772e27d5dee50f'
            'f22e0165aacb2bc86d000728c81f68022abcc601dbfd09e516e1ba772225d7e6'
            'ddc26e8e6dcce1a51f8aa8ed33072afcf4ff2d20f64ea24d96409479e86e8750'
            '0b4614530fc331af175a00dcc48eeae4a2f0239112693fafda946381f8e33321')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  for p in "${_patches[@]}"; do
    patch --input="$srcdir/$(echo "$p"|cut -d: -f1)"
  done
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
  cp "$srcdir/config.h" config.h
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    TERMINFO="$pkgdir/usr/share/terminfo" \
    install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
